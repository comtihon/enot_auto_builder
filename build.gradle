buildscript {
    ext {
        springBootVersion = '2.5.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

version = '0.1.1'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    baseName = 'enot_auto_builder'
    version = version
}
ext {
    metaModelsSourcesDir = file('src/generated/java')
}

springBoot {
    buildInfo()
}

configurations {
    querydslapt
}

sourceSets {
    main {
        java {
            srcDir metaModelsSourcesDir
        }
    }
    test_integration {
        java {
            compileClasspath = sourceSets.main.output + configurations.testRuntime
            runtimeClasspath = output + compileClasspath
            srcDir file('src/test_integration/java')
        }
        resources.srcDir file('src/test_integration/resources')
    }
}

task querymodels(type: JavaCompile, group: 'build') {
    doFirst {
        delete metaModelsSourcesDir
        metaModelsSourcesDir.mkdirs()
    }

    classpath = configurations.compile + configurations.querydslapt
    destinationDir = metaModelsSourcesDir

    source = sourceSets.main.java
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor",
            "-s", metaModelsSourcesDir.toString()
    ]
}

task test_integration(type: Test) {
    testClassesDir = sourceSets.test_integration.output.classesDir
    classpath = sourceSets.test_integration.runtimeClasspath
}

idea {
    module {
        sourceDirs += metaModelsSourcesDir
        generatedSourceDirs += metaModelsSourcesDir
    }
}

compileJava.dependsOn querymodels
check.dependsOn test_integration

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-jdbc')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.integration:spring-integration-mail')
    compile('org.springframework.integration:spring-integration-core')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('com.zaxxer:HikariCP')
    compile('org.modelmapper:modelmapper:1.1.0')
    compile('javax.mail:javax.mail-api')
    compile('javax:javaee-api:7.0')
    compile('commons-io:commons-io:2.5')
    compile('org.eclipse.jgit:org.eclipse.jgit:4.8.0.201706111038-r')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('com.github.metadave:etp:0.4')
    compile('com.querydsl:querydsl-jpa:4.1.4')
    compile('org.projectlombok:lombok:1.16.18')
    compile('com.querydsl:querydsl-apt:4.1.4')
    compile('org.apache.commons:commons-compress:1.14')

    runtime('org.postgresql:postgresql')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('com.h2database:h2:1.4.196')
    testCompile('org.mockito:mockito-all:1.10.19')
}
